(ns carlos.core
  (:require [reagent.core :as r]
            [carlos.svg :refer [illustration]]
            [dommy.core :refer-macros [sel1]]))

(def config (r/atom {:seed       (rand-int 1e7)
                     :complexity 10
                     :animation  true}))

(defn set-seed! [s]
  (swap! config #(assoc % :seed s))
  (-> (sel1 :#seed)
      (.-value)
      (set! s))
  (println "Seed set to" (:seed @config)))

(defn seed-changed! []
  (-> (sel1 :#seed)
      (.-value)
      (set-seed!)))

(defn random-seed! [ev]
  (set-seed! (rand-int 1e7))
  (.preventDefault ev))

(defn set-complexity! [c]
  (swap! config #(assoc % :complexity c))
  (println "Complexity set to" (:complexity @config)))

(defn set-animation! [c]
  (swap! config #(assoc % :animation c))
  (println "Animation set to" (:animation @config)))

(defn render! []
  (let [data (-> (r/render-to-string [illustration @config])
                 (clojure.string/replace
                   #"<svg "
                   (str "<svg "
                        "xmlns=\"http://www.w3.org/2000/svg\" "
                        "xmlns:svg=\"http://www.w3.org/2000/svg\" "
                        "xmlns:xlink=\"http://www.w3.org/1999/xlink\" "
                        "width=\"720\" height=\"720\" ")))
        blob (js/Blob. (clj->js [data]) #js {:type "data:image/svg+xml;"})]
    (doto (js/document.createElement "a")
      (aset "href" (js/URL.createObjectURL blob))
      (aset "download" (str "carlos-" (:seed @config) ".svg"))
      (js/document.body.appendChild)
      (.click)
      (js/document.body.removeChild))))

;; -------------------------
;; Views

(defn home-page []
  (let [header-hidden (r/atom false)]
    (fn []
      [:div
       [:a#show-hide {:on-click #(swap! header-hidden not)} "â—¹"]
       (when-not @header-hidden
         [:header
          [:h1 "Carlos"]
          "Autogenerated SVG illustrations inspired by "
          [:a {:href "https://en.wikipedia.org/wiki/Carlos_Cruz-Diez" :target "_blank"} "Carlos Cruz-Diez"]
          "' Physichromies. "
          [:a {:href "https://github.com/polymeris/carlos" :target "_blank"} "Code on Github"]
          "."
          [:form
           [:label "Seed "
            [:input {:id            :seed
                     :type          :number
                     :default-value (:seed @config)
                     :placeholder   "Seed"
                     :on-change     seed-changed!}]
            [:button {:id       :random-seed
                      :on-click random-seed!}
             "Randomize"]]
           [:label [:input {:id        :animate
                            :type      :checkbox
                            :on-change #(set-animation! (-> (sel1 :#animate)
                                                            (.-checked)))
                            :checked   (:animation @config)}]
            "Animate"]
           [:a#download {:on-click render!} "Download SVG"]]])
       [:div#illustration
        {:on-click random-seed!}
        [illustration @config]]])))

;; -------------------------
;; Initialize app

(defn mount-root []
  (r/render [home-page] (.getElementById js/document "app")))

(defn init! []
  (mount-root))
